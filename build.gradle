/*
 * Copyright 2019 - 2020 Alexengrig Dev.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'io.codearte.nexus-staging' version '0.30.0'
}

group 'dev.alexengrig'
version '1.0-SNAPSHOT'

description 'An util library for getting information about a method invocation'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

test {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                name = project.name
                description = project.description
                url = 'https://github.com/alexengrig/whereisfrom'
                licenses {
                    license {
                        name = 'Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'alexengrig'
                        name = 'Grig Alex'
                        email = 'alexengrigdev@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/alexengrig/whereisfrom.git'
                    developerConnection = 'scm:git:https://github.com/alexengrig/whereisfrom.git'
                    url = 'https://github.com/alexengrig/whereisfrom.git'
                }
            }
        }
    }
    repositories {
        maven {
            url = version.toString().endsWith('SNAPSHOT')
                    ? 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
                    : 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username = project.properties['ossrhUsername'].toString()
                password = project.properties['ossrhPassword'].toString()
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

nexusStaging {
    serverUrl = 'https://s01.oss.sonatype.org/service/local/'
    username = project.properties['ossrhUsername'].toString()
    password = project.properties['ossrhPassword'].toString()
}
